!!! FEA R3: Static analysis of a 2D curved beam structure
!! Initial setup
! White canvas  
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15

! Units: [N],[Nmm],[N/mm^2=MPa],[N/mm]

!! Constant parameters
A=40
B=60
P0=10 ! [kN/m] = [N/mm]
F1=5000
M0=1500E3
ELAST=210E3
NU=0.3

!! SETUP: element types, materials, sections
KEYW,PR_STRUC,1  ! Preferences / Structural
/PREP7  
ET,1,BEAM188
! Order of interpolation 0 - linear, 2 - quadratic, 3 - cubic
KEYOPT,1,3,0
! Define the I-section (I80)
SECTYPE,1,BEAM,I 
SECOFFSET, CENT 
SECDATA,42,42,80,5.9,5.9,3.9
! Define the solid rectangle section
SECTYPE,2,BEAM,RECT
SECOFFSET, CENT 
SECDATA,B,A
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU

!! Modelling
K,1,-1000,0
K,2,0,0
K,3,0,1000
K,4,1000,1000
K,5,1000,0
K,6,1000,1500
K,7,1400,1500
K,8,2000,1500
L,3,4
L,5,4
L,4,6
L,6,7
L,7,8
LARC,1,3,2,1000	! Create the arc

!! Meshing
! Set the number of elements on the lines
LESIZE,1,,,10
LESIZE,2,,,10
LESIZE,3,,,6
LESIZE,4,,,6
LESIZE,5,,,6
LESIZE,6,,,12
! Mesh the beams with I-section
SECNUM,1
LMESH,2,5
! Mesh the beams with rectangle section
SECNUM,2
LMESH,1
LMESH,6

!! Constraints and Loads
! Define the kinematic constraints
DK,1,UY,0
DK,5,UY,0
DK,5,UX,0
DK,8,ALL,0
! Plane model, constrain UZ of all nodes
D,ALL,UZ,0
! Constant load on curved beam
LSEL,S,,,6
ESLL,S  
SFBEAM,ALL,2,PRES,P0
ALLSEL,ALL 
! Ramped load on line 1
! Select carefully the first and last element numbers!
ENUM1ST=29
ENUMLAST=38
*DO, ENUM, ENUM1ST, ENUMLAST
! Define the ramped load on the ENUMth element
SFBEAM, ENUM, 2, PRES, 	P0*(1 - (ENUM - ENUM1ST)/(ENUMLAST - ENUM1ST + 1)),	P0*(1 - (ENUM - ENUM1ST + 1)/(ENUMLAST - ENUM1ST + 1))
*ENDDO
! Bending moment
FK,6,MZ,-M0
! Force
FK,7,FY,-F1
FINISH

!! Solution: solve the finite element equation K.U = F
/SOL
SOLVE

!! Postprocessing
/POST1
! Define the element tables
ETABLE,FXI,SMISC, 1 
ETABLE,FXJ,SMISC, 14
ETABLE,MZI,SMISC, 3 
ETABLE,MZJ,SMISC, 16
ETABLE,SFYI,SMISC, 6 
ETABLE,SFYJ,SMISC, 19
ETABLE,EXI,SMISC, 7 
ETABLE,EXJ,SMISC, 20
ETABLE,EPELBYTI,SMISC, 42 
ETABLE,EPELBYTJ,SMISC, 47
ETABLE,EPELBYBI,SMISC, 43 
ETABLE,EPELBYBJ,SMISC, 48
ETABLE,SDIRI,SMISC, 31 
ETABLE,SDIRJ,SMISC, 36
ETABLE,SBYTI,SMISC, 32
ETABLE,SBYTJ,SMISC, 37
ETABLE,SBYBI,SMISC, 33 
ETABLE,SBYBJ,SMISC, 38

! Plot the element tables on the structure

PLLS,FXI,FXJ,1,0,0	! <a href="images/1.png" target="_blank">Click here to view image</a>
PLLS,MZI,MZJ,1,0,0	! <a href="images/2.png" target="_blank">Click here to view image</a>
PLLS,SFYI,SFYJ,1,0,0	! <a href="images/3.png" target="_blank">Click here to view image</a>
PLLS,SFYI,SFYJ,1,0,0	! <a href="images/4.png" target="_blank">Click here to view image</a>
PLLS,EXI,EXJ,1,0,0	! <a href="images/5.png" target="_blank">Click here to view image</a> 
PLLS,EPELBYTI,EPELBYTJ,1,0,0	! <a href="images/6.png" target="_blank">Click here to view image</a>
PLLS,EPELBYBI,EPELBYBJ,1,0,0	! <a href="images/7.png" target="_blank">Click here to view image</a>
PLLS,SDIRI,SDIRJ,1,0,0	! <a href="images/8.png" target="_blank">Click here to view image</a>
PLLS,SBYTI,SBYTJ,1,0,0	! <a href="images/9.png" target="_blank">Click here to view image</a>
PLLS,SBYBI,SBYBJ,1,0,0	! <a href="images/10.png" target="_blank">Click here to view image</a>