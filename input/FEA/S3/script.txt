!!! FEA S3: Plate with rectangular holes
! Created by Tamas Poloskei, 2017. February 10.
! Units: [N],[Nmm],[N/mm^2=MPa],[N/mm]

!! Define constant parameters
T=20
ELAST=2E5
NU=0.33
RHO=7.89E-9
P=20
R=50
G=9810

!! SETUP: element types, materials, sections
KEYW,PR_STRUC,1  ! Preferences / Structural
/PREP7  
ET,1,PLANE182   
! Set key options for PLANE182 electing plane stress with thickness
KEYOPT,1,3,3
! Define the thickness (Real Constant set)
R,1,T
! Material Properties
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU  
MPDATA,DENS,1,,RHO  

!! Modelling
! Create keypoints
K,1,400,200
K,2,400,800
K,3,1400,800
K,4,1400,200
! Create lines
L,1,2
L,2,3
L,3,4
L,4,1
! Create line fillets
LFILLT,1,2,R
LFILLT,2,3,R
LFILLT,3,4,R
LFILLT,4,1,R
! Create the area using lines
AL,1,5,2,6,3,7,4,8
! Copy the area 3 times
AGEN,3,1,,,1300
! Create the largest rectangle area
BLC4,0,0,3500,1000  
! Subtract the three smaller rectangle from the large one
ASBA,4,1 
ASBA,5,2
ASBA,1,3

!! Meshing
! Smart size = 2
SMRT,2  
! Quad elements, area
MSHAPE,0,2D 
! Free meshing
MSHKEY,0
AMESH,2
! Refine the mesh
AREFINE,2,,,1

!! Constraints and Loads
! Kinematic constraints
DL,28,,ALL,0  
DL,31,,UX,0 
DL,32,,UX,0   
! Load case 1: distributed load
SFL,27,PRES,P,
! Write loadstep
LSWRITE,1
! Delete the load
SFLDELE,27,PRES   
! Load case 2: gravitational field
ACEL,0,G,0
! Write loadstep
LSWRITE,2 
FINISH

!! Solve the finite element equation K.U = F, for loadstep 1 and 2
/SOL
LSSOLVE,1,2,1,  

!! Postprocessing
/POST1  
! Create the load cases
LCDEF,1,1,LAST, 
LCDEF,2,2,LAST, 
! Read external load and add the own weight
LCASE,1,
LCOPER,ADD,2
! Define path by two locations
PATH,PATH1,2,30,100,
PPATH,1,0,7000/2,0,0,0, 
PPATH,2,0,7000/2,1000,0,0
PDEF,UY,U,Y,AVG 
PDEF,SX,S,X,AVG 
PDEF,SY,S,Y,AVG 
PDEF,SXY,S,XY,AVG   
! Plot the diagrams on the path and saving them as pictures in the project's folder
! Change the background color to white
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
PLPATH,UY   
/IMAGE,SAVE,u_y,bmp
PLPATH,SX 
/IMAGE,SAVE,sigma_x,bmp
PLPATH,SY   
/IMAGE,SAVE,s_y,bmp
PLPATH,SXY
/IMAGE,SAVE,tau_xy,bmp    