!!! FEA D2: TRANSIENT DYNAMIC ANALYSIS OF A BUILT-IN PLATE

!! Initial setup
! White canvas
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
! Isometric view
/VIEW,1,1,1,1
/ANG,1
/REP,FAST
! Define constant parameters
! Units: [N],[Nmm],[N/mm^2=MPa],[N/mm]

A=800
B=400
T=10
E1=2E5
nu1=0.3
rho=8E-3
v0=20
ELEMNUM=50
TEND=1
DELT=5e-3

!! SETUP: element types, materials,
/PREP7  
ET,1,SHELL181
! Defining thickness
R,1,t,  

MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,E1 
MPDATA,PRXY,1,,NU1 
MPTEMP  
MPTEMP,1,0  
MPDATA,DENS,1,,RHO  

! Create keypoints
K,1,0,0,0
K,2,0,0,B
K,3,A,0,B
K,4,A,0,0

A,1,2,3,4

LESIZE,1,,,ELEMNUM
LESIZE,3,,,ELEMNUM
LESIZE,2,,,2*ELEMNUM
LESIZE,4,,,2*ELEMNUM

! mesh the areas using mapped algorithm
MSHAPE,0,2D 
MSHKEY,1
AMESH,1

DL,1, ,ALL,0 
FINISH 

/SOL
! transient analysis  
ANTYPE,4
TRNOPT,FULL 
LUMPM,0 
!* timestep size
DELTIM,DELT,0,0
OUTRES,ALL,ALL
! auto timesteppinng
AUTOTS,1
! stiffness matrix coefficient
BETAD,0.0002
KBC,0

TIME,0.025
DL,1, ,UY,v0 
LSWRITE,1,

TIME,0.05 
DL,1,,ALL,0 
LSWRITE,2,
TIME,TEND
LSWRITE,3

LSSOLVE,1,3,1,

/POST1
/VIEW,1,1,2,3
/ANG,1
! animate results
PLNSOL,U,Y
ANTIME,500,0.1, ,1,0,0,0