!ttl
! FEA D7: Impact loading on a beam

! Created by Gergely Gyebroszki, 2018.04.18.

!par
! Units in SI: [N],[N/m^2=Pa],[N/m]

H=0.025
E=2E11
NU=0.32
RHO=7850
A=6.25E-4
IZ=32.552E-9

! Max value of Fy

FRCY=450

! Time when Fy reaches its maximum value

T1=0.0001

! Time when Fy reaches zero

T2=0.0002

! Ending time of simulation

T3=0.5
SUBSTEPS1=20
SUBSTEPS2=20
SUBSTEPS3=200
DAMPING=0.0005

!par
! SETUP: Element types, Materials, Sections

KEYW,PR_STRUC,1
/PREP7

!  BEAM3 element type is not available in the GUI

ET,1,BEAM3
MPTEMP,1,0
MPDATA,EX,1,,E
MPDATA,PRXY,1,,NU
MPDATA,DENS,1,,RHO
R,1,A,IZ,H

!par
! MODELLING: Geometry and meshing

K,,-0.5 
K,,0.5
L,1,2
LESIZE,1,,,30
LMESH,ALL

! Select nodes at X=-0.5 and also select at X=0.5

NSEL,S,LOC,X,-0.5   
NSEL,A,LOC,X,0.5

! We can use D on ALL nodes when applying constraints, but don't forget to select everything afterwards

D,ALL,ALL
ALLSEL,ALL
FINISH  

!par
! SOLUTION: Time history is given by 3 time points

/SOL
ANTYPE,4 ! 4: Transient
! Time point at T1
TRNOPT,FULL 
LUMPM,0 
AUTOTS,0 ! Turn off auto time stepping
NSUBST,SUBSTEPS1,0,0  
OUTRES,ALL,1
TIME,T1
NSEL,S,LOC,X,0  
F,ALL,FY,-FRCY
ALLSEL,ALL
LSWRITE,1

! Also create a version of this load step with damping

BETAD,DAMPING
LSWRITE,4

! Time point at T2

TIME,T2
NSUBST,SUBSTEPS2,0,0
BETAD,0
FDELE,ALL,FY
LSWRITE,2

! Also create a version of this load step with damping

BETAD,DAMPING
LSWRITE,5

! Time point at T3

TIME,T3
BETAD,0
NSUBST,SUBSTEPS3,0,0  
LSWRITE,3

! Also create a version of this load step with damping

BETAD,DAMPING

! Stop at 0.25 sec instead of 0.5

TIME,T3/2
LSWRITE,6

! Solve from Load Step files

LSSOLVE,1,3,1
FINISH

!par
! TIME HISTORY POSTPROCESSING

/POST26
NSOL,2,NODE(0,0,0),U,Y,UY_CENTER
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
PLVAR,2

!skip
/IMAGE,SAVE,Case_A_No_Damping,bmp

!fig
! D7-1.png

FINISH

!par
! CASE B: With Damping

/SOL
LSSOLVE,4,6,1
FINISH  
/POST26
NSOL,2,NODE(0,0,0),U,Y,UY_CENTER

! Change the color of Curve 1

/COLOR,CURVE,BMAG,1
PLVAR,2

!skip
/IMAGE,SAVE,Case_B_With_Damping,bmp

!fig
D7-2.png

FINISH