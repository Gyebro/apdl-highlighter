! FEA T1: ANALYSIS OF AN AXISYMMETRIC DRUM
!! Initial setup
! White canvas
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
! Define constant parameters
! Units: [N],[Nmm],[N/mm^2=MPa],[N/mm]
D1 = 40
D2 = 50
D3 = 60
D4 = 100
D5 = 160
R1 = 20
R2 = 15
H1 = 40
H2 = 100
TH = 5
ELAST = 2.1E5
NU = 0.33
RHO = 7.86E-9
DELTA = 0.005
OM = 200

!! SETUP: element types, materials,
/PREP7
ET,1,PLANE182   
! set axysimmetric option
KEYOPT,1,3,1

MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU

MPTEMP
MPTEMP,1,0  
MPDATA,DENS,1,,rho  

! create keypoints
K,1,D1/2, H1, 0
K,2,D2/2, H1, 0
K,3,D1/2, 0, 0
K,4,D1/2, TH, 0
K,5,D3/2, 0, 0
K,6,D5/2-TH-R1, 0, 0
K,7,D5/2-TH-R1, R1 + TH, 0
K,8,D5/2, R1 + TH, 0
K,9,D5/2, H2-TH-R2, 0
K,10,D5/2-TH-R2, H2, 0
K,11,D4/2, H2, 0
K,12,D5/2-TH-R2, H2-TH-R2, 0

! create lines
L,4,3
L,3,5
L,5,6
LARC,6,8,7,R1+TH
L,8,9
LARC,9,10,12,R2+TH
L,10,11

ADRAG,1,,,,,,2,3,4,5,6,7
A,1,2,13,4
AGLUE,ALL

! set element size on specific lines for mapped mesh
LESIZE,1,,,8
*DO, III,8,23,3
LESIZE,III,,,8
*ENDDO
!*
! proportional element size
*DO, III,9,24,3
LESIZE,III,TH/8
*ENDDO
LESIZE,28,TH/8

! create mapped mesh on every area
MSHKEY,1
AMESH,ALL

DK,3,UY,0
!! load case 1
DL,1,,UX,DELTA  
DL,28,,UX,DELTA 
LSWRITE,1

!! load case 2
! apply global angular velocity about the Y axis
DLDELE,1,UX
DLDELE,28,UX 
OMEGA,0,OM,0
LSWRITE,2
FINISH

/SOL
LSSOLVE,1,2,1
FINISH

/POST1
LCDEF,1,1,Last, 
LCDEF,2,2,Last,
! 1st load case
LCASE,1,
PLNSOL, S,INT, 2,1.0
/IMAGE,SAVE,S_INT_LCASE_1,bmp
PLNSOL, U,X, 2,1.0
/IMAGE,SAVE,DISP_X_LCASE_1,bmp

! 2nd load case
LCASE,2,
PLNSOL, S,INT, 2,1.0
/IMAGE,SAVE,S_INT_LCASE_2,bmp
PLNSOL, U,X, 2,1.0
/IMAGE,SAVE,DISP_X_LCASE_2,bmp

! 1st and 2nd load case
LCASE,1,
LCOPER,ADD,2
PLNSOL, U,X, 2,1.0
/IMAGE,SAVE,DISP_X_LCASE_12,bmp
FINISH  

! modal analysis with prestress effect
/PREP7
DLDELE,1,UX
DLDELE,28,UX 
OMEGA,0,200,0
FINISH

! calculate initial stress state
/SOL
ANTYPE,0
PSTRES,1
SOLVE
FINISH

! solve modal analysis using prestress effect
/SOL
ANTYPE,2  
MODOPT,LANB,10  
EQSLV,SPAR  
MXPAND,10, , ,1 
LUMPM,0 
PSTRES,1  
MODOPT,LANB,10,0,0, ,OFF
SOLVE   
FINISH  

/POST1
SET,FIRST
*DO,III,1,10
PLNSOL, U,SUM, 2,1.0
/IMAGE,SAVE,strcat(chrval(III),'_eigenshape'),bmp
SET,NEXT
*ENDDO