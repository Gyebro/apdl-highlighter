! Initial setup
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15

L0 = 1
A1 = 500e-6
A2 = 300e-6

I1 = 1e-6
I2 = 3e-7

H1 = 56e-3
H2 = 37e-3

Exx = 210e9
NU = 0.3
P0 = 2e3

*AFUN, DEG

! sine and cosine of the angle alpha
SA = SIN(60)
CA = COS(60)

/PREP7
ET,1,BEAM3
R,1,A1,I1,H1
R,2,A2,I2,H2

! define material properties
MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,Exx
MPDATA,PRXY,1,,NU

K,1,CA*L0,0
K,2,(CA+1)*L0,0
K,3,(2*CA+1)*L0,SA*L0
K,4,(CA+1)*L0,2*SA*L0
K,5,CA*L0,2*SA*L0
K,6,0,SA*L0
K,7,L0,SA*L0

*DO,I,1,6
L,I,I+1
*ENDDO
L,7,3
L,6,1

LESIZE,ALL,,,1

LSEL,S,,,1,4,3
LMESH,ALL

LSEL,INVE
REAL,2
LMESH,ALL

ALLSEL,ALL

/ESHAPE,5.0
/VIEW,1,1,2,3
/REPLOT

DK,7,ALL,0

LSEL,S,,,1,4,3
ESLL,S  
SFBEAM,ALL,1,PRES,-P0
ALLSEL,ALL

FINISH
/SOL
SOLVE
/POST1
/ESHAPE,0.0
/VIEW,1,,,1 
/ANG,1
PLNSOL,U,Y,1,1.0
/IMAGE,SAVE,disp_y,png
!
! axial stress
ETABLE,SDIRI,LS,1
ETABLE,SDIRJ,LS,4
!
! bending stress +y
ETABLE,SBYTI,LS,2
ETABLE,SBYTJ,LS,5
!
! bending stress -y
ETABLE,SBYBI,LS,3
ETABLE,SBYBJ,LS,6
!
! maximum stress (normal + bending)
ETABLE,SMAXI,NMISC, 1
ETABLE,SMAXJ,NMISC, 3
!
! minimum stress (normal + bending)
ETABLE,SMINI,NMISC, 2
ETABLE,SMINJ,NMISC, 4
!
! section force, normal
ETABLE,MFORXI,SMISC, 1
ETABLE,MFORXJ,SMISC, 7
!
! section force, shear
ETABLE,MFORYI,SMISC, 2
ETABLE,MFORYJ,SMISC, 8
!
! section force, moment
ETABLE,MMOMZI,SMISC, 6
ETABLE,MMOMZJ,SMISC, 12
!
! axial strain
ETABLE,EPELDIRI,LEPEL, 1
ETABLE,EPELDIRJ,LEPEL, 4
!
! bending strain +y
ETABLE,EPELBYTI,LEPEL, 2
ETABLE,EPELBYTJ,LEPEL, 5
!
! bending strain -y
ETABLE,EPELBYBI,LEPEL, 3
ETABLE,EPELBYBJ,LEPEL, 6
!
PLLS,SDIRI,SDIRJ,1,0,0
/IMAGE,SAVE,axial_stress,png
!
PLLS,SBYTI,SBYTJ,1,0,0
/IMAGE,SAVE,bending_stress_ty,png
!
PLLS,SBYBI,SBYBJ,1,0,0
/IMAGE,SAVE,bending_stress_by,png
!
PLLS,SMAXI,SMAXJ,1,0,0
/IMAGE,SAVE,max_stress,png
!
PLLS,SMINI,SMINJ,1,0,0
/IMAGE,SAVE,min_stress,png
!
PLLS,MFORXI,MFORXJ,1,0,0
/IMAGE,SAVE,normal_force,png
!
PLLS,MFORYI,MFORYJ,1,0,0
/IMAGE,SAVE,shear_force,png
!
PLLS,MMOMZI,MMOMZJ,1,0,0
/IMAGE,SAVE,bending_moment,png
!
PLLS,EPELDIRI,EPELDIRJ,1,0,0
/IMAGE,SAVE,axial_strain,png
!
PLLS,EPELBYTI,EPELBYTJ,1,0,0
/IMAGE,SAVE,bending_strain_ty,png
!
PLLS,EPELBYBI,EPELBYBJ,1,0,0
/IMAGE,SAVE,bending_strain_by,png