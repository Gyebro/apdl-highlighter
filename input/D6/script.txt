!!! FEA D6: Dynamic analysis of a weightlifting bar
!! Initial setup
! White canvas
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 

! Define constant parameters
! Units: [N],[Nmm],[N/mm^2=MPa],[N/mm]
ELAST=210E9
NU=0.3
RHO=7800
G=9.81
R=0.014
T=0.410826
D=0.4
L1=0.1
L2=0.4
L3=1.6

!! SETUP: element types, real constants, materials
KEYW,PR_STRUC,1  ! Preferences / Structural
/PREP7
! Element type 1
ET,1,BEAM188
!
SECTYPE,1,BEAM,CSOLID,,0 
SECOFFSET,CENT 
SECDATA,R
! Element type 2
ET,2,PLANE182   
KEYOPT,2,3,3

R,1,T

MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU

MPTEMP,1,0  
MPDATA,DENS,1,,RHO

!! Modeling
BLC4,0,0,L1,D
BLC4,L1+L3,0,L1,D
!
K,,0,D/2
K,,L1,D/2
K,,L1+L2,D/2
K,,L1+L3-L2,D/2
K,,L1+L3,D/2
K,,2*L1+L3,D/2

*DO,III,9,13
L,III,III+1
*ENDDO

!! Meshing
LESIZE,ALL,0.0125
AESIZE,ALL,0.0125
TYPE,1
LMESH,9,13
TYPE,2
AMESH,ALL
!
NUMMRG,NODE,0.001, , ,LOW
!
/ESHAPE,1

!! Definition of loads
! Loadstep 1
NSEL,S,LOC,Y,0  
D,ALL,ALL,0
!
NSEL,S,LOC,X,L1+L3/2
D,ALL,UX,0

ALLSEL,ALL
D,ALL,UZ,0

ACEL,0,G,0
FINISH  

!! Transient analysis
/SOL
!
ANTYPE,4

TRNOPT,FULL

ANTYPE,4
NLGEOM,1
DELTIM,0.01,0.001,0.1
OUTRES,ALL,ALL  
AUTOTS,1
ALPHAD,6
BETAD,0.0002
TIME,0.2
KBC,0

SOLVE

DDELE,ALL,UY

! Time step 1
TIME,1.0
DELTIM,0.002,0.0002,0.02
DK,11,UY,1.2
DK,12,UY,1.2
LSWRITE,1

! Time step 2
TIME,1.3
DK,11,UY,0.9
DK,12,UY,0.9
LSWRITE,2

! Time step 3
TIME,2.0
DK,11,UY,0.9
DK,12,UY,0.9
LSWRITE,3

! Time step 4
TIME,3.0
DK,11,UY,1.5
DK,12,UY,1.5
LSWRITE,4

! Time step 5
TIME,4.5
DK,11,UY,1.5
DK,12,UY,1.5
LSWRITE,5

! Time step 6
TIME,5.0
DK,11,UY,1.4
DK,12,UY,1.4
LSWRITE,6

! Time step 7
TIME,5.5
DK,11,UY,1.95
DK,12,UY,1.95
LSWRITE,7

! Time step 8
TIME,6.5
DK,11,UY,1.95
DK,12,UY,1.95
LSWRITE,8
LSSOLVE,1,8,1,  
FINISH

!! Post processing
/POST26
NSOL,2,NODE(L1+L3/2,D/2,0),U,Y, UY_2,
NSOL,3,NODE(L1+L3/2,D/2,0),V,Y, VY_3,
NSOL,4,NODE(L1+L3/2,D/2,0),A,Y, AY_4,
/XRANGE,0,6.5
/AXLAB,X,'t [s]'

/AXLAB,Y,'uY [m]'
PLVAR,2,	! <a href="images/1.png" target="_blank">Click here to view image</a>

/AXLAB,Y,'vY [m/s]'
PLVAR,3,	! <a href="images/2.png" target="_blank">Click here to view image</a>

/AXLAB,Y,'aY [m/s^2]'
PLVAR,4,	! <a href="images/3.png" target="_blank">Click here to view image</a>