!!! VEMA 04. HÉT RÁCSOS SZEREZET DEFORMÁCIÓJÁNAK SZÁMÍTÁSA VÉGESELEMES MÓDSZERREL
!! Kezdeti beállítások
! Fehér háttér
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/ESHAPE,1
! Dimenziók: [N],[Nm],[N/m^2=Pa],[N/m]

! Paraméterek definiálása
A=4
B=3
C=2
D=1
BETA=60
V=10E-3
DK=70E-3
EACEL=210E9
EKOTEL=1E9
NU=0.3
G=6000
PI=acos(-1)
! Preprocesszálás
/PREP7
!! Beállítások (elemtípus, anyagi viselkedés, keresztmetszet)
! Elemtípus kiválasztása
ET,1,LINK180
! KeresztmetszetEK definiálása
SECTYPE,1,LINK, ,ACEL
SECDATA,V**2,   

SECTYPE,2,LINK, ,KOTEL
SECDATA,DK**2*PI/4,   

! Anyagi viselkedés definiálása
MPTEMP,1,0  
MPDATA,EX,1,,EACEL 
MPDATA,PRXY,1,,NU 

MPTEMP,1,0  
MPDATA,EX,2,,EKOTEL  
MPDATA,PRXY,2,,NU

!! Modellezés
! Kulcspontok létrehozása
K,1,B,0
K,2,0,0
K,3,B,A
K,4,D,C

! Vonalak létrehozása
L,1,3
L,1,4
L,3,4
L,2,4
! <a href="images/1.png" target="_blank">Aktuális képernyőkép</a>
!! Hálózás
! Egy darab végeselem minden vonalon
LESIZE,ALL,,,1
! Acél rudak hálózása
MAT,1
SECNUM,1
LMESH,1,2
! Kötelek hálózása
MAT,2
SECNUM,2
LMESH,3,4
! <a href="images/2.png" target="_blank">Aktuális képernyőkép</a>
!! Kényszerek definiálása
! Megfogás
D,NODE(0,0,0),ALL,0
D,NODE(B,0,0),ALL,0
! Aktív külső terhelés
! Trigonometrikus függények fok dimenziójú argumentummal
*AFUN,DEG
F,NODE(B,A,0),FX,G*sin(BETA)
F,NODE(B,A,0),FY,-G*cos(BETA)
FINISH
! <a href="images/3.png" target="_blank">Aktuális képernyőkép</a>
!! K.U=F egyenletrendszer megoldása
/SOL
SOLVE
FINISH
!! Posztprocesszálás
/POST1
! Deformált alak
PLDISP,2	! <a href="images/4.png" target="_blank">Aktuális képernyőkép</a>
! X irányú elmozdulás
PLNSOL, U,X, 1,1.0	! <a href="images/5.png" target="_blank">Aktuális képernyőkép</a>
! Y irányú elmozdulás
PLNSOL, U,Y, 1,1.0	! <a href="images/6.png" target="_blank">Aktuális képernyőkép</a>

! Elem táblázatok definiálása
! Rúdirányú normálfeszültségek
ETABLE,SXXI,LS, 1
ETABLE,SXXJ,LS, 2
! Normálerő definiálása
ETABLE,FN,SMISC, 1
! Normálfeszültség eloszlás megjelenítése [Pa]
PLLS,SXXI,SXXJ,1,0,0	! <a href="images/7.png" target="_blank">Aktuális képernyőkép</a>
! Normálerő eloszlás [N]
PLLS,FN,FN,1,0,0	! <a href="images/8.png" target="_blank">Aktuális képernyőkép</a>
! Reakcióerők listázása
PRRSOL