!!! VEMA 06. HÉT: Statikailag határozatlan síkgörbe rúd
!! Kezdeti beállítások
! Fehér háttér
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/ESHAPE,1
! Dimenziók: [N],[Nm],[N/m^2=Pa],[N/m]

! Paraméterek definiálása
R=1
D=20e-3
ELAST=200e9
NU=0.3
P=3000
ELEMMERET=0.1
! Preprocesszálás
/PREP7
!! Beállítások (elemtípus, anyagi viselkedés, keresztmetszet)
! Elemtípus kiválasztása
ET,1,BEAM188
! Interpolációs polinom rendje az elem mentén
! 0 - lináris (alaphelyzet), 2 - négyzetes, 3 - köbös
KEYOPT,1,3,2

! Anyagi viselkedés definiálása
MPTEMP,1,0  
MPDATA,EX,1,,ELAST
MPDATA,PRXY,1,,NU 

! Keresztmetszetek definiálása
SECTYPE,   1, BEAM, CSOLID
SECOFFSET, CENT 
SECDATA,D

!! Modellezés
! Kulcspontok létrehozása
K,1,0,0
K,2,R,0
K,3,R,R
K,4,3*R,R
! Vonalak létrehozása
LARC,1,3,2,R
L,3,4
GPLOT! <a href="images/1.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! Hálózás
! 'ELEMMERET' átlagos hosszúságú végeselem minden vonalon
LESIZE,ALL,ELEMMERET
LMESH,ALL
!! Kényszerek definiálása
! Befalazott végek
D,NODE(0,0,0),ALL,0
D,NODE(3*R,R,0),ALL,0
! Rúdra merőleges irányú megoszló terhelés definiálása
LSEL,S,LINE,,2
ESLL,S  
SFBEAM,ALL,2,PRES,P
ALLSEL,ALL
FINISH
! <a href="images/2.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! K.U=F egyenletrendszer megoldása
/SOL
SOLVE
FINISH
!! Posztprocesszálás
/POST1
! Deformált alak
PLDISP,2! <a href="images/3.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! X irányú elmozdulás
PLNSOL, U,X, 1,1.0! <a href="images/4.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Y irányú elmozdulás
PLNSOL, U,Y, 1,1.0! <a href="images/5.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>

! Elem táblázatok definiálása
! z tengely körüli hajlítónyomaték
ETABLE,MZI,SMISC, 3
ETABLE,MZJ,SMISC, 16
! y tengely irányú nyírófeszültség definiálása
ETABLE,VYI,SMISC, 6
ETABLE,VYJ,SMISC, 19
! Hajlításból származó normálfeszültség a felső szélső szálban
ETABLE,SBYTI,SMISC, 32
ETABLE,SBYTJ,SMISC, 37
! Hajlítónyomatéki függvény megjelenítése [Nm]
PLLS,MZI,MZJ,1,0,0! <a href="images/6.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Nyíróerő eloszlás megjelenítés [N]
PLLS,VYI,VYJ,1,0,0! <a href="images/7.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Hajlításból származó normálfeszültség eloszlása a felső szálban [Pa]
PLLS,SBYTI,SBYTJ,1,0,0! <a href="images/8.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Reakcióerők listázása
PRRSOL