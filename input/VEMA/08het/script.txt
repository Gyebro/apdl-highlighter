!!! VEMA 08. HÉT: Vastagfalú tartály modellezése forgászszimmetrikus elemekkel.
!! Kezdeti beállítások
! Fehér háttér
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/ESHAPE,1
! Dimenziók: [N],[Nm],[N/m^2=Pa],[N/m]

! Paraméterek definiálása
RBELSO=100e-3
RKULSO=200e-3
H=300e-3
ELAST=200e9
NU=0.3
P=300e5
ELEMMERET=0.005
! Preprocesszálás
/PREP7
!! Beállítások (elemtípus, anyagi viselkedés, keresztmetszet)
! Elemtípus kiválasztása
ET,1,PLANE182
! Forgászszimmetrikus elemi viselkedés kiválasztása
KEYOPT,1,3,1
! Anyagi viselkedés definiálása
MPTEMP,1,0  
MPDATA,EX,1,,ELAST
MPDATA,PRXY,1,,NU 

!! Modellezés
! Kulcspontok létrehozása
K,1,RBELSO,0
K,2,RKULSO,0
K,3,RBELSO,H
K,4,RKULSO,H
K,5,0,H
K,6,0,H+RBELSO
K,7,0,H+RKULSO
! Vonalak létrehozása
L,1,2
L,2,4
L,4,3
L,3,1
L,6,7
LARC,3,6,5,RBELSO
LARC,4,7,5,RKULSO
! Felületek létrehozása
AL,1,2,3,4
AL,3,7,5,6
GPLOT! <a href="images/1.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! Hálózás
! 'ELEMMERET' átlagos elemméret
AESIZE,ALL,ELEMMERET
! strukturált stílusú hálózási algoritmus
MSHKEY,1
AMESH,ALL
!! Kényszerek definiálása
! szimmetria síkban görgős megtámasztás
DL,1,,UY,0
! Aktív külső terhelés
! belső nyomás felületen
SFL,4,PRES,P
SFL,6,PRES,P
FINISH
! <a href="images/2.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! K.U=F egyenletrendszer megoldása
/SOL
SOLVE
FINISH
!! Posztprocesszálás
/POST1
! Deformált alak
PLDISP,2! <a href="images/3.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! X (radiális) irányú normálfeszültség [Pa]
PLNSOL, S,X, 2,1.0  ! <a href="images/4.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Y (axiális) irányú normálfeszültség [Pa]
PLNSOL, S,Y, 2,1.0! <a href="images/5.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! z (tangenciális) irányú normálfeszültség [Pa]
PLNSOL, S,Z, 2,1.0! <a href="images/6.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Mohr-féle egyenértékű feszültség [Pa]
PLNSOL, S,INT, 2,1.0! <a href="images/7.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Feszültség eloszlás a vízszintes szimmetria sík mentén
PATH,SZIMM,2,30,20
PPATH,1,0,RBELSO,0
PPATH,2,0,RKULSO,0

PDEF,SRAD,S,X,AVG
PDEF,SAX,S,Y,AVG 
PDEF,STAN,S,Z,AVG 

/AXLAB,X,'r [m]'
/AXLAB,Y,'sig [Pa]'
PLPATH,SRAD,SAX,STAN! <a href="images/8.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>

PDEF,SIGMOHR,S,INT,AVG
PDEF,SIGHMH,S,EQV,AVG

PLPATH,SIGMOHR,SIGHMH! <a href="images/9.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>

! Feszültség eloszlás a belső fal mentén
/AXLAB,X,'y [m]'
PATH,BELSO,2,30,20
PPATH,1,0,RBELSO,0
PPATH,2,0,RBELSO,H

PDEF,SRAD,S,X,AVG
PDEF,SAX,S,Y,AVG 
PDEF,STAN,S,Z,AVG 
PLPATH,SRAD,SAX,STAN! <a href="images/10.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>

! Feszültségeloszlás a gömbsüveg forgástengelyben található keresztmetszete mentén
/AXLAB,X,'r [m]' 
PATH,GOMB,2,30,20
PPATH,1,0,0,RBELSO+H
PPATH,2,0,0,RKULSO+H

PDEF,SMER,S,X,AVG
PDEF,STAN,S,Z,AVG 
PLPATH,SMER,STAN! <a href="images/11.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>

! Feszültségeloszlás a gömbsüveg és a hengeres rész találkozásának mentén
PATH,GOMBHENG,2,30,20
PPATH,1,0,RBELSO,H
PPATH,2,0,RKULSO,H

PDEF,SMER,S,Y,AVG
PDEF,STAN,S,Z,AVG 
PLPATH,SMER,STAN! <a href="images/12.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>