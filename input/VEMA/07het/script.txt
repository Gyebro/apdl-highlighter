!!! VEMA 07. HÉT: Kéttámaszú tartó modellezése síkelemekkel
!! Kezdeti beállítások
! Fehér háttér
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/ESHAPE,1
! Dimenziók: [N],[Nm],[N/m^2=Pa],[N/m]

! Paraméterek definiálása
A=0.5
L=2
H=0.3
B=25e-3
ELAST=200e9
NU=0.3
F=3200
ELEMMERET=0.05
! Preprocesszálás
/PREP7
!! Beállítások (elemtípus, anyagi viselkedés, keresztmetszet)
! Elemtípus kiválasztása
ET,1,PLANE182
! Síkfeszültségi vastagsággal állapot definiálása 
KEYOPT,1,3,3
! Vastagság definiálása
R,1,B
! Anyagi viselkedés definiálása
MPTEMP,1,0  
MPDATA,EX,1,,ELAST
MPDATA,PRXY,1,,NU 

!! Modellezés
! Kulcspontok létrehozása
K,1,0,0
K,2,A,0
K,3,A+L/2,0
K,4,A+L/2,H
K,5,0,H
! Vonalak létrehozása
*DO,III,1,4,1
L,III,III+1
*ENDDO
L,5,1
AL,1,2,3,4,5
GPLOT! <a href="images/1.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! Hálózás
! 'ELEMMERET' átlagos elemméret
AESIZE,ALL,ELEMMERET
! 'szabad' stílusú hálózási algoritmus
MSHKEY,0
AMESH,ALL
!! Kényszerek definiálása
! szimmetria síkban görgős megtámasztás
DL,3,,UX,0
! A pontban görgős megtámasztás
D,NODE(A,0,0),UY,0
! Aktív külső terhelés
! Koncentrált terhelés
F,NODE(0,H,0),FY,-F
FINISH
! <a href="images/2.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! K.U=F egyenletrendszer megoldása
/SOL
SOLVE
FINISH
!! Posztprocesszálás
/POST1
! Deformált alak
PLDISP,2! <a href="images/3.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! X irányú normálfeszültség
PLNSOL, S,X, 2,1.0  ! <a href="images/4.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Y irányú normálfeszültség
PLNSOL, S,Y, 2,1.0! <a href="images/5.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! XY irányú csúsztatófeszültség
PLNSOL, S,Y, 2,1.0! <a href="images/6.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>

! Útvonal menti feszültségeloszlás megjelentítése
! x irányú normálfeszültség a szimmetria síkban
PATH,MY_PATH,2,30,20, 
PPATH,1,0,A+L/2,0,0,0
PPATH,2,0,A+L/2,H,0,0

PDEF,SX,S,X,AVG

! Diagramon megjelenítés
/AXLAB,X,'yCRD [m]'
/AXLAB,Y,'sigX [Pa]'
PLPATH,SX
! Szerkezeten megjelenítés
PLPAGM,SX,1,Blank
! <a href="images/sx.gif" target="_blank">Kattints ide az x irányú normálfeszültség eloszlás változásának megtekintéséhez!</a>

! Csúsztatófeszültség x = A/2-ben
PATH,MY_PATH,2,30,20, 
PPATH,1,0,A/2,0,0,0
PPATH,2,0,A/2,H,0,0

PDEF,SXY,S,XY,AVG

! Diagramon megjelenítés
/AXLAB,Y,'tauXY [Pa]'
PLPATH,SXY
! Szerkezeten megjelenítés
PLPAGM,SXY,1,Blank
! <a href="images/sxy.gif" target="_blank">Kattints ide az xy síkban ébredő csúsztatófeszültés eloszlás változásának megtekintéséhez!</a>
! y irányú normálfeszültség x = A-ban
PATH,MY_PATH,2,30,20, 
PPATH,1,0,A,0,0,0
PPATH,2,0,A,H,0,0

PDEF,SY,S,Y,AVG

! Diagramon megjelenítés
/AXLAB,Y,'sigY [Pa]'
PLPATH,SXY
! Szerkezeten megjelenítés
PLPAGM,SXY,1,Blank
! <a href="images/sy.gif" target="_blank">Kattints ide az y irányú normálfeszültség eloszlás változásának megtekintéséhez!</a>