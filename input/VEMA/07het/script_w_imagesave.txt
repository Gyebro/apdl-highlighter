!!! VEMA 07. HÉT: Kéttámaszú tartó modellezése síkelemekkel
!! Kezdeti beállítások
! Fehér háttér
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/ESHAPE,1
! Dimenziók: [N],[Nm],[N/m^2=Pa],[N/m]

! Paraméterek definiálása
A=0.5
L=2
H=0.3
B=25e-3
ELAST=200e9
NU=0.3
F=3200
ELEMMERET=0.05
! Preprocesszálás
/PREP7
!! Beállítások (elemtípus, anyagi viselkedés, keresztmetszet)
! Elemtípus kiválasztása
ET,1,PLANE182
! Síkfeszültségi vastagsággal állapot definiálása 
KEYOPT,1,3,3
! Vastagság definiálása
R,1,B
! Anyagi viselkedés definiálása
MPTEMP,1,0  
MPDATA,EX,1,,ELAST
MPDATA,PRXY,1,,NU 

!! Modellezés
! Kulcspontok létrehozása
K,1,0,0
K,2,A,0
K,3,A+L/2,0
K,4,A+L/2,H
K,5,0,H
! Vonalak létrehozása
*DO,III,1,4,1
L,III,III+1
*ENDDO
L,5,1
AL,1,2,3,4,5
GPLOT! <a href="images/1.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! Hálózás
! 'ELEMMERET' átlagos elemméret
AESIZE,ALL,ELEMMERET
! 'szabad' stílusú hálózási algoritmus
MSHKEY,0
AMESH,ALL
!! Kényszerek definiálása
! szimmetria síkban görgős megtámasztás
DL,3,,UX,0
! A pontban görgős megtámasztás
D,NODE(A,0,0),UY,0
! Aktív külső terhelés
! Koncentrált terhelés
F,NODE(0,H,0),FY,-F
FINISH
! <a href="images/2.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
!! K.U=F egyenletrendszer megoldása
/SOL
SOLVE
FINISH
!! Posztprocesszálás
/POST1
! Deformált alak
PLDISP,2! <a href="images/3.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! X irányú normálfeszültség
PLNSOL, S,X, 2,1.0  ! <a href="images/4.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! Y irányú normálfeszültség
PLNSOL, S,Y, 2,1.0! <a href="images/5.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>
! XY irányú csúsztatófeszültség
PLNSOL, S,Y, 2,1.0! <a href="images/6.png" target="_blank">Kattints ide az aktuális képernyőkép megtekintéséhez!</a>

/COLOR,SMIN,BLUE
/COLOR,SMAX,RED
/AXLAB,X,'Ycrd [m]' 
*DO,XCRD,0,1.5,0.01
PATH,MY_PATH,2,30,20, 
PPATH,1,0,XCRD,0,0,0
PPATH,2,0,XCRD,H,0,0

PDEF,SX,S,X,AVG
PDEF,SY,S,Y,AVG 
PDEF,SXY,S,XY,AVG

/AXLAB,Y,'sigX [Pa]'
/YRANGE,-5e6,5e6
PLPATH,SX
/image,save,strcat('SXPATH_',chrval(1000*XCRD)),png

/AXLAB,Y,'sigY [Pa]'
/YRANGE,-7e5,3e5
PLPATH,SY
/image,save,strcat('SYPATH_',chrval(1000*XCRD)),png

/AXLAB,Y,'tauXY [Pa]'
/YRANGE,-2e5,8e5
PLPATH,SXY
/image,save,strcat('SXYPATH_',chrval(1000*XCRD)),png

PADEL,ALL
*ENDDO

/GROPT,VIEW,1
/FOCUS,0,(A+L/2)/2,H/2,0
/DIST,ALL,0.7,0
*DO,XCRD,0,1.5,0.01
PATH,MY_PATH,2,30,20, 
PPATH,1,0,XCRD,0,0,0
PPATH,2,0,XCRD,H,0,0

PDEF,SX,S,X,AVG
PDEF,SY,S,Y,AVG 
PDEF,SXY,S,XY,AVG

/CONT,1,9,-5e6, ,5e6
PLPAGM,SX,1,Blank
/image,save,strcat('SXGEOM_',chrval(1000*XCRD)),png

/CONT,1,9,-7e5, ,3e5
PLPAGM,SY,1,Blank
/image,save,strcat('SYGEOM_',chrval(1000*XCRD)),png

/CONT,1,9,-2e5,,8e5
PLPAGM,SXY,1,Blank
/image,save,strcat('SXYGEOM_',chrval(1000*XCRD)),png

PADEL,ALL
*ENDDO