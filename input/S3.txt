!* FEA computer laboratory R3 problem
!*
!* STATIC ANALYSIS OF A 2D CURVED BEAM STRUCTURE
!*
!* created by Tamas Poloskei
!* 2017. February 10.
!*
!* covert everything to a consistent unit system
!* [N],[Nmm],[N/mm^2=MPa],[N/mm]
!*
t = 20
elast = 2E5
nu = 0.33
rho = 7.89E-9
p = 20
r = 50
g = 9810

/COM,  Structural

/PREP7  
!* element type definition
!*  
ET,1,PLANE182   
!*  selecting plane stress with thickness
KEYOPT,1,1,0
KEYOPT,1,3,3
KEYOPT,1,6,0
!*  
!*  defining the thickness
R,1,t,  
!*
!* define the material properties
!* elasticity
!* Temperatures must be defined first
MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU  
!* density
!* Temperatures must be defined first
MPTEMP  
MPTEMP,1,0  
MPDATA,DENS,1,,RHO  
!*
!* create keypoints
K,1,400,200
K,2,400,800
K,3,1400,800
K,4,1400,200
!*
!* create lines
L,1,2
L,2,3
L,3,4
L,4,1
!*
!* create line fillets
LFILLT,1,2,r
LFILLT,2,3,r
LFILLT,3,4,r
LFILLT,4,1,r
!*
!* create the area using lines
AL,1,5,2,6,3,7,4,8
!*
!* copy the area 3 times
AGEN,3,1,,,1300
!*
!* create the largest rectangle area
BLC4,0,0,3500,1000  
!* subtract the three smaller rectangle from the large one
ASBA,4,1 
ASBA,5,2
ASBA,1,3
!*
!* smart size = 2
SMRT,2  
!* quad elements, area
MSHAPE,0,2D 
!* free
MSHKEY,0
AMESH,2
!*
!* refine the mesh
AREFINE,2,,,1
!*
!* kinematic constraints
DL,28,,ALL,0  
DL,31,,UX,0 
DL,32,,UX,0   
!* load case 1. distributed load
SFL,27,PRES,p,
!* write loadstep
LSWRITE,1
!* delete the load
SFLDELE,27,PRES   
!* load case 2. gravitational field
ACEL,0,g,0
!* write loadstep
LSWRITE,2 
FINISH
!* solve the finite element equation K.U = F, for loadstep 1 and 2
/SOL
LSSOLVE,1,2,1,  
!* postprocessing
/POST1  
!* create the load cases
LCDEF,1,1,Last, 
LCDEF,2,2,Last, 
!*
!* read external load and add the own weight
LCASE,1,
LCOPER,ADD,2
!*
!* define path by two locations
PATH,PATH1,2,30,100,
PPATH,1,0,7000/2,0,0,0, 
PPATH,2,0,7000/2,1000,0,0
PDEF,UY,U,Y,AVG 
PDEF,SX,S,X,AVG 
PDEF,SY,S,Y,AVG 
PDEF,SXY,S,XY,AVG   
!*
!* plotting the diagrams on the path and saving them as pictures in the project's folder
!* be aware that on my computer the size of the pictures were ~1.4MB
!* changing the background color to white
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
PLPATH,UY   
/IMAGE,SAVE,u_y,bmp
PLPATH,SX 
/IMAGE,SAVE,sigma_x,bmp
PLPATH,SY   
/IMAGE,SAVE,s_y,bmp
PLPATH,SXY
/IMAGE,SAVE,tau_xy,bmp    