!* FEA computer laboratory R3 problem
!*
!* STATIC ANALYSIS OF A 2D CURVED BEAM STRUCTURE
!*
!* created by Tamas Poloskei
!* 2017. February 10.
!*
!* covert everything to a consistent unit system
!* [N],[Nmm],[N/mm^2=MPa],[N/mm]
!*
A = 40
B = 60
P0 = 10 !* [kN/m] = [N/mm]
F1 = 5000
M0 = 1500E3
ELAST = 210E3
NU = 0.3

/COM,  Structural

/PREP7  
!*  set element type
ET,1,BEAM188
!*
!* define the I-section (I80)
SECTYPE,1,BEAM,I 
SECOFFSET, CENT 
SECDATA,42,42,80,5.9,5.9,3.9
!*
!* define the solid rectangle section
SECTYPE,2,BEAM,RECT
SECOFFSET, CENT 
SECDATA,B,A
!*
!* define the material properties
!* elasticity
!* Temperatures must be defined first
MPTEMP
MPTEMP,1,0  
MPDATA,EX,1,,ELAST  
MPDATA,PRXY,1,,NU
!* 
!* defining the keypoints
K,1,-1000,0
K,2,0,0
K,3,0,1000
K,4,1000,1000
K,5,1000,0
K,6,1000,1500
K,7,1400,1500
K,8,2000,1500
!*
!* create the lines
L,3,4
L,5,4
L,4,6
L,6,7
L,7,8
!*
!* create the arc
LARC,1,3,2,1000
!*
!* setting the number of elements on the lines
LESIZE,1,,,10
LESIZE,2,,,10
LESIZE,3,,,6
LESIZE,4,,,6
LESIZE,5,,,6
LESIZE,6,,,12
!*
!* meshing the beams with I-section
SECNUM,1
LMESH,2,5
!*
!* meshing the beams with rectangle section
SECNUM,2
LMESH,1
LMESH,6
!*
!* defining the kinematic constraints
DK,1,UY,0
DK,5,UY,0
DK,5,UX,0
DK,8,ALL,0
!*
!* plane model
D,ALL,UZ,0
!*
!* define loads
!*
!* constant load on curved beam
LSEL,S,,,6
ESLL,S  
SFBEAM,ALL,2,PRES,P0
ALLSEL,ALL 
!*
!* ramped load on line 1
!* select carefully the first and last element numbers!
ENUM1ST = 29
ENUMLAST = 38
*DO, ENUM, ENUM1ST, ENUMLAST
	!* define the ramped load on the ENUMth element
	SFBEAM, ENUM, 2, PRES, 	P0*(1 - (ENUM - ENUM1ST)/(ENUMLAST - ENUM1ST + 1)),	P0*(1 - (ENUM - ENUM1ST + 1)/(ENUMLAST - ENUM1ST + 1))
*ENDDO
!*
!* bending moment
FK,6,MZ,-M0
!*
!* force
FK,7,FY,-F1
FINISH
!* solve the finite element equation K.U = F
/SOL
SOLVE
!* postprocessing
/POST1
!* define the element tables
ETABLE,FXI,SMISC, 1 
ETABLE,FXJ,SMISC, 14
!*
ETABLE,MZI,SMISC, 3 
ETABLE,MZJ,SMISC, 16
!*
ETABLE,SFYI,SMISC, 6 
ETABLE,SFYJ,SMISC, 19
!*
ETABLE,EXI,SMISC, 7 
ETABLE,EXJ,SMISC, 20
!*
ETABLE,EPELBYTI,SMISC, 42 
ETABLE,EPELBYTJ,SMISC, 47
!*
ETABLE,EPELBYBI,SMISC, 43 
ETABLE,EPELBYBJ,SMISC, 48
!*
ETABLE,SDIRI,SMISC, 31 
ETABLE,SDIRJ,SMISC, 36
!*
ETABLE,SBYTI,SMISC, 32 
ETABLE,SBYTJ,SMISC, 37
!*
ETABLE,SBYBI,SMISC, 33 
ETABLE,SBYBJ,SMISC, 38
!* plotting the element tables on the structure and saving them as pictures in the project's folder
!* be aware that on my computer the size of the pictures were ~1.4MB
!* changing the background color to white
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15 
!*
PLLS,FXI,FXJ,1,0,0  
/IMAGE,SAVE,axial_force,bmp  
!*
PLLS,MZI,MZJ,1,0,0  
/IMAGE,SAVE,bending_moment,bmp  
!*
PLLS,SFYI,SFYJ,1,0,0  
/IMAGE,SAVE,shear_force,bmp  
!*
PLLS,SFYI,SFYJ,1,0,0  
/IMAGE,SAVE,shear_force,bmp  
!*
PLLS,EXI,EXJ,1,0,0  
/IMAGE,SAVE,axial_strain,bmp  
!*
PLLS,EPELBYTI,EPELBYTJ,1,0,0  
/IMAGE,SAVE,bending_strain_top,bmp  
!*
PLLS,EPELBYBI,EPELBYBJ,1,0,0
/IMAGE,SAVE,bending_strain_bottom,bmp
!*
PLLS,SDIRI,SDIRJ,1,0,0
/IMAGE,SAVE,axial_stress,bmp
!*
PLLS,SBYTI,SBYTJ,1,0,0
/IMAGE,SAVE,bending_stress_top,bmp
!*
PLLS,SBYBI,SBYBJ,1,0,0
/IMAGE,SAVE,bending_stress_bottom,bmp
